%LET SAHIE=/Directory/;
LIBNAME SAHIE "&SAHIE.";

FILENAME CSV2008 SASZIPAM "&SAHIE.SAHIE_2008.CSV.ZIP";
FILENAME CSV2009 SASZIPAM "&SAHIE.SAHIE_2009.CSV.ZIP";
FILENAME CSV2010 SASZIPAM "&SAHIE.SAHIE_2010.CSV.ZIP";
FILENAME CSV2011 SASZIPAM "&SAHIE.SAHIE_2011.CSV.ZIP";
FILENAME CSV2012 SASZIPAM "&SAHIE.SAHIE_2012.CSV.ZIP";
FILENAME CSV2013 SASZIPAM "&SAHIE.SAHIE_2013.CSV.ZIP";
FILENAME CSV2014 SASZIPAM "&SAHIE.SAHIE_2014.CSV.ZIP";

%MACRO SAHIE_IMPORT(YY);
DATA SAHIE.PART_SAHIE&YY.;
	RETAIN year	version	statefips	countyfips	geocat	agecat	racecat	sexcat	iprcat	NIPR	nipr_moe	NUI	nui_moe	NIC	nic_moe	PCTUI	pctui_moe	PCTIC	pctic_moe	PCTELIG	pctelig_moe	PCTLIIC	pctliic_moe	state_name	county_name;
	LENGTH year 4. statefips $2. countyfips $3. state_name $20. county_name $45. agecat 3. racecat 3. sexcat 3. iprcat 3. NIPR 5. NUI 5. PCTUI 3. ;
	INFILE CSV20&YY.(sahie_20&YY..csv) DLM="," FIRSTOBS=81 DSD LRECL=252;
	INPUT year	version $	statefips $	countyfips $	geocat $	agecat	racecat	sexcat	iprcat	NIPR	nipr_moe	NUI	nui_moe	NIC	nic_moe	PCTUI	pctui_moe	PCTIC	pctic_moe	PCTELIG	pctelig_moe	PCTLIIC	pctliic_moe	state_name $	county_name $;
	IF GEOCAT = '50' & AGECAT = "0" & RACECAT = "0" & SEXCAT = "0" & IPRCAT = "0";
RUN;
%MEND;

%SAHIE_IMPORT(08);
%SAHIE_IMPORT(09);
%SAHIE_IMPORT(10);
%SAHIE_IMPORT(11);
%SAHIE_IMPORT(12);
%SAHIE_IMPORT(13);
%SAHIE_IMPORT(14);

DATA SAHIE.SAHIE_050;
	FORMAT GEOID $5.;
	RETAIN GEOID YEAR STATE_NAME COUNTY_NAME NIPR NUI PCTUI;
	SET SAHIE.PART_SAHIE:;
	GEOID = COMPRESS(STATEFIPS||COUNTYFIPS);
	DROP STATEFIPS COUNTYFIPS;
	ARRAY NUMB(*) _NUMERIC_;
	DO i = 1 TO DIM(NUMB);
		IF NUMB(i) = . THEN NUMB(i) = 0;
	END;
	DROP i;
RUN;

PROC SORT DATA=SAHIE.SAHIE_050;
	BY GEOID YEAR;
RUN;

%LET VIZ=/Directory/;
FILENAME SAHIEDBF "&VIZ.SAHIE.DBF";
PROC DBF DATA=SAHIE.SAHIE_050 DB4=SAHIEDBF;
RUN;
