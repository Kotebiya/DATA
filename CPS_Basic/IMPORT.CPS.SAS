/* Section 1: Setup */
%LET SOURCE=/folders/myshortcuts/DATA/Census/CPS Basic/;
LIBNAME BASIC "&SOURCE.";

FILENAME INSTR "&SOURCE.SAS_IMPORT.XLS";
PROC IMPORT DATAFILE=INSTR OUT=BASIC.INSTRUCT DBMS=XLS REPLACE;
RUN;

/* Section 2: Importing everything from raw files */
DATA _NULL_;
	SET BASIC.INSTRUCT;
	*WHERE YEAR IN("2000","2001","2002","2003","2004",
		"2005","2006","2007","2008","2009");
	WHERE YEAR IN("2007","2008");
	CALL EXECUTE(
	"FILENAME datafile SASZIPAM ""&SOURCE."||TRIM(DICTIONARY)||"/"||TRIM(MONTH)||TRIM(SUBSTR(TRIM(YEAR),3,2))||"pub.zip"";
 	DATA BASIC.BASE_"||TRIM(YEAR)||"_"||TRIM(MONNUM)||";
 	INFILE datafile("||TRIM(MONTH)||TRIM(SUBSTR(TRIM(YEAR),3,2))||"pub."||TRIM(FILETYPE)||") LRECL=2300 MISSOVER;
 		%INCLUDE ""&SOURCE.IMPORT."||TRIM(DICTIONARY)||".SAS"";
 	RUN;
	");
RUN;

/* Section 3: Recording, Transforming, and Appending */
%LET KEEPVARS =HRHHID HRHHID2 PULINENO HRMONTH HRYEAR4 HRMIS
	PESEX PRTAGE PEAGE PTDTRACE PEHSPNON PEMARITL PRCITSHP
	PERACE PRHSPNON
	PERRP HEFAMINC
	PEEDUCA PESCHENR PEDWAVR
	PEMLR PEHRUSLT PEHRFTPT PELKDUR PENLFACT
	HWHHWGT PWSSWGT
	HETENURE HEHOUSUT;

DATA FREQ00;
	SET	BASIC.BASE_2000:
		BASIC.BASE_2001:
		BASIC.BASE_2002:
		BASIC.BASE_2003:
		BASIC.BASE_2004:;
	KEEP &KEEPVARS.;
	WHERE PWSSWGT > 0;
RUN;

DATA FREQ05;
	SET	BASIC.BASE_2005:
		BASIC.BASE_2006:
		BASIC.BASE_2007:
		BASIC.BASE_2008:
		BASIC.BASE_2009:;
	KEEP &KEEPVARS.;
	WHERE PWSSWGT > 0;
RUN;

DATA FREQ10;
	SET	BASIC.BASE_2010:
		BASIC.BASE_2011:
		BASIC.BASE_2012:
		BASIC.BASE_2013:
		BASIC.BASE_2014:;
	KEEP &KEEPVARS.;
	WHERE PWSSWGT > 0;
RUN;

DATA FREQ14;
	SET	BASIC.BASE_2015:
		BASIC.BASE_2016:;
	KEEP &KEEPVARS.;
	WHERE PWSSWGT > 0;
RUN;

DATA FREQ;
	SET	FREQ00
		FREQ05
		FREQ10
		FREQ14;
RUN;
